10  REM SPDX-FILECOPYRIGHTTEXT: (C) 2025 RYAN CARSTEN SCHMIDT <HTTPS://GITHUB.COM/RYANDESIGN>
20  REM SPDX-LICENSE-IDENTIFIER: MIT
30  HOME : GR
40 A$ = "MERRY CHRISTMAS"
50 X1 = 19:Y1 = 1:Y2 = 33:Y3 = 36:Y4 = 39
60 TI = 23:BI = 8:NB = 40:FI = 10
70 A = 0:B = 0:J = 0:K = 0:D = 0
80  DIM BX(NB): DIM BY(NB): DIM BC(NB)
90  VTAB 22: HTAB X1 +2 - LEN(A$)/2
100  REM STAR
110  COLOR= 13:X = X1:Y = Y1
120  PLOT X,Y
200  REM NEEDLES
210  COLOR= 4:L = X:R = X:I = 0
220 Y = Y +1:I = I +1
240  IF I >2  THEN I = 0:L = L -1:R = R +1
250  GOSUB 900
260  IF Y <Y2  THEN 220
300  REM TRUNK
310  COLOR= 8
320 Y = Y +1: PLOT X1,Y
330  IF Y <Y3  THEN 320
400  REM BASE
410  COLOR= 1
420 Y = Y +1: HLIN X1 -1,X1 +1 AT Y
430  IF Y <Y4  THEN 420
500  REM BLINK UNTIL KEYPRESS
510 I = 0
520 I = I +1: IF I <FI  THEN 580
530 RB =  INT( RND(1) *NB):BC = BC(RB)
540  IF BC <16  THEN  COLOR= BC:BC = BC +16: GOTO 570
550  COLOR= 4:BC = BC -16
570  PLOT BX(RB),BY(RB):BC(RB) = BC:I = 0
580  IF  PEEK(49152) <128  THEN 520
590  POKE 49168,0: TEXT : HOME : END
900  REM DRAW HLINE
910  IF D = 0  THEN S = L:E = R:ST = 1: GOTO 930
920 S = R:E = L:ST =  -1
930 D = 1 -D
940  FOR X = S TO E  STEP ST
950  PLOT X,Y
960 J = J +1: IF J >TI  THEN J = 0:A = A +1: PRINT  MID$ (A$,A,1);
970 K = K +1: IF K >BI  THEN K = 0:BX(B) = X:BY(B) = Y:BC(B) =  INT( RND(1) *14) +1:B = B +1
980  NEXT
990  RETURN
